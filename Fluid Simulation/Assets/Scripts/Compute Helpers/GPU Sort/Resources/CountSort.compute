#pragma kernel ClearCounts
#pragma kernel CalculateCounts
#pragma kernel ScatterOutput
#pragma kernel CopyBack

struct Entry
{
	uint hash;
	uint key;
};

// Actual counting sort algorithm below
RWStructuredBuffer<Entry> InputKeys;
RWStructuredBuffer<uint> InputValues;
RWStructuredBuffer<Entry> SortedKeys;
RWStructuredBuffer<uint> SortedValues;

RWStructuredBuffer<uint> Counts;
uint numInputs;

static const int GroupSize = 128;

[numthreads(GroupSize, 1, 1)]
void ClearCounts(uint3 id : SV_DispatchThreadID)
{
	if (id.x >= numInputs) return;

	Counts[id.x] = 0;
}

[numthreads(GroupSize, 1, 1)]
void CalculateCounts(uint3 id : SV_DispatchThreadID)
{
	if (id.x >= numInputs) return;

	Entry e = InputKeys[id.x];
	uint key = e.key;
	InterlockedAdd(Counts[key], 1);
}

[numthreads(GroupSize, 1, 1)]
void ScatterOutput(uint3 id : SV_DispatchThreadID)
{
	if (id.x >= numInputs) return;

	Entry e = InputKeys[id.x];
	uint key = e.key;
	uint value = InputValues[id.x];

	uint sortedIndex;
	InterlockedAdd(Counts[key], 1, sortedIndex);

	SortedKeys[sortedIndex] = e;
	SortedValues[sortedIndex] = value;
}

[numthreads(GroupSize, 1, 1)]
void CopyBack(uint3 id : SV_DispatchThreadID)
{
	if (id.x >= numInputs) return;

	InputKeys[id.x] = SortedKeys[id.x];
	InputValues[id.x] = SortedValues[id.x];
}