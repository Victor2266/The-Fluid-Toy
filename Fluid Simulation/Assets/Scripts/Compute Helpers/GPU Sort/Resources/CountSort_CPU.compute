#pragma kernel ClearCounts
#pragma kernel CalculateCounts
#pragma kernel ScatterOutput
#pragma kernel CopyBack

// Actual counting sort algorithm below
RWStructuredBuffer<uint> InputKeys;
RWStructuredBuffer<uint> InputValues;
RWStructuredBuffer<uint> SortedKeys;
RWStructuredBuffer<uint> SortedValues;

RWStructuredBuffer<uint> Counts;
uint numInputs;

RWStructuredBuffer<uint> KeyArr; // For CPU-GPU split

static const int GroupSize = 128;

[numthreads(GroupSize, 1, 1)]
void ClearCounts(uint3 id : SV_DispatchThreadID)
{
	if (id.x >= numInputs) return;

	Counts[id.x] = 0;
}

[numthreads(GroupSize, 1, 1)]
void CalculateCounts(uint3 id : SV_DispatchThreadID)
{
	if (id.x >= numInputs) return;

	uint key = InputKeys[id.x];
	InterlockedAdd(Counts[key], 1);
}

[numthreads(GroupSize, 1, 1)]
void ScatterOutput(uint3 id : SV_DispatchThreadID)
{
	if (id.x >= numInputs) return;

	uint key = InputKeys[id.x];
	uint value = InputValues[id.x];

	uint sortedIndex;
	InterlockedAdd(Counts[key], 1, sortedIndex);

	SortedKeys[sortedIndex] = key;
	SortedValues[sortedIndex] = value;
}

[numthreads(GroupSize, 1, 1)]
void CopyBack(uint3 id : SV_DispatchThreadID)
{
	if (id.x >= numInputs) return;

	InputKeys[id.x] = SortedKeys[id.x];
	InputValues[id.x] = SortedValues[id.x];
	KeyArr[id.x] = SortedKeys[id.x];
}